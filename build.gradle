plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java'
    id 'maven-publish'
}

group 'de.davidcrome.gradle'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    shadow 'commons-io:commons-io:2.6'
    implementation 'commons-io:commons-io:2.6'

    shadow 'com.github.docker-java:docker-java:3.2.1'
    implementation 'com.github.docker-java:docker-java:3.2.1'

    implementation 'com.google.inject:guice:4.2.3'
}

shadowJar {
    archiveClassifier = ''

    // This config includes the Guice dependency and also the google transient dependencies (Guava, ...).
    // Just including the Guice dependencies does not help at all
    // because the transitive dependencies will be missing at runtime.
    include(dependency {
        it.moduleGroup.contains('com.google')
    })

    // The transitive deps must also be added! Lucky me these deps do not have any further runtime dependencies,
    // otherwise I need to add also the deps of the deps ;)
    include(dependency('javax.inject:javax.inject'))
    include(dependency('aopalliance:aopalliance'))
    include(dependency('org.ow2.asm:asm'))
    include(dependency('cglib:cglib'))

    // But only relocate Guice
    relocate 'com.google.inject', 'de.davidcrome.gradle.shadow_plugin_example.com.google.inject'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
